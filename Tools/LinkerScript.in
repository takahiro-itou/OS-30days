
OUTPUT_FORMAT(binary)
OUTPUT_ARCH(i386)

ENTRY(HariMain)

CODE_SEGMENT_BASE   = 0x00000000;
DATA_SEGMENT_BASE   = @__DATSEG_BASE__@;
SIZE_FOR_MALLOC     = @__MALLOC_SIZE__@;
BASE_FOR_MALLOC     = @__MALLOC_BASE__@;
DATA_SEGMENT_SIZE   = @__DATSEG_SIZE__@;

MEMORY {
    ROM(rx)  : ORIGIN = CODE_SEGMENT_BASE, LENGTH = 1024K
    RAM(rwx) : ORIGIN = DATA_SEGMENT_BASE, LENGTH = 1024K
};

SECTIONS {
    .  =  ORIGIN(ROM);
    .header : {
        LONG(DATA_SEGMENT_SIZE);
        LONG(0x69726148);
        LONG(0x00000000);
        LONG(DATA_SEGMENT_BASE);
        LONG(_END_DATA - _START_DATA);
        LONG(_OFFSET_DATA);
        LONG(0xe9000000);
        LONG(HariMain - . - 4);
        LONG(BASE_FOR_MALLOC);
    } > ROM

    .text       : {
        *(.text)
        . = ALIGN(16);
        _size_of_text = .;
    } > ROM

    _OFFSET_DATA    = .;
    .data       (DATA_SEGMENT_BASE) :
    {
        _START_DATA = .;
        *(.data)
        *(.data.*)
        . = ALIGN(16);
    } > RAM  AT > ROM

    .rodata     . : {
        *(.rodata);
        *(.rodata.*);
        . = ALIGN(16);
    } > RAM  AT > ROM

    .bss       . : {
        *(.bss);
        *(.bss.*);
        *(COMMON);
        . = ALIGN(16);
        LONG(0);
        _END_DATA   = .;
    } > RAM  AT > ROM

}
